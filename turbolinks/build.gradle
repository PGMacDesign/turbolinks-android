apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
group='com.github.PGMacDesign'

buildscript {
    ext.libVersionName = '1.2.0'

    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.0'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 11
        versionName "$libVersionName"

        // Define ProGuard rules for this android library project. These rules will be applied when
        // a consumer of this library sets 'minifyEnabled true'.
        consumerProguardFiles 'proguard-consumer-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api 'androidx.appcompat:appcompat:1.0.2'
    api 'com.google.code.gson:gson:2.8.6'
    api 'org.apache.commons:commons-lang3:3.4'
    //    api 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    testImplementation 'org.assertj:assertj-core:1.7.0'
    testImplementation 'org.robolectric:robolectric:3.1.2'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'junit:junit:4.12'
}

ext {

    publishedGroupId = 'com.pgmacdesign'
    artifact = 'modifiedturbolinks'

    libraryName = 'Modified Turbolinks Android'
    libraryDescription = 'Modified Turbolinks for Android'
    libraryVersion = "$libVersionName"

    siteUrl = 'https://github.com/PGMacDesign/turbolinks-android'
    gitUrl = 'https://github.com/PGMacDesign/turbolinks-android.git'

}

// Maven
group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

